/******************************************************************************************
*
* MAD-X File for PS Booster optics calculations
*
*
* PSB Extraction working point:
* New working point: QX = 4.172 and QY = 4.23.  21 Dec 2006 O.Berrig
*
*
* This file is for protons at 0.348 GeV/c at time c = 301
*
*
* Execute with:  >madx < psb_orbit.madx
*
******************************************************************************************/

 title, 'BOOSTER lattice';

 !for the time being not used
 beam_Ek=%(KINETICENERGY)s;
 beam_Etot = beam_Ek + pmass;
 beam_pc = sqrt(beam_Etot*beam_Etot - pmass*pmass);
 

 option, echo;
 option, RBARC=FALSE;

/******************************************************************************************
 * BOOSTER
 ******************************************************************************************/
 call, file = '%(FILES_DIR)s/psb.ele';
 call, file = '%(FILES_DIR)s/psb.seq';
 call, file = '%(FILES_DIR)s/defseries.seq';
 call, file = '%(FILES_DIR)s/powering.seq';
 call, file = '%(FILES_DIR)s/psb.dbx';
 call, file = '%(FILES_DIR)s/psb_orbit.str';


/******************************************************************************************
 * beam, use
 ******************************************************************************************/
 
 
 beam, particle=PROTON, pc=beam_pc;  ! change depending on c-time of orbit measurement; pc=momentum

 
 use, sequence=psb%(RING)s;

/******************************************************************************************
 * Match for new working point
 ******************************************************************************************/

 Qx = %(NAT_TUNE_X)s;
 Qy = %(NAT_TUNE_Y)s;
 Qxd = %(DRV_TUNE_X)s;
 Qyd = %(DRV_TUNE_Y)s;

 hacmap21 = 0;
 vacmap43 = 0;


 MATCH,sequence=psb%(RING)s;
  vary, NAME=kKF, step = 0.0001;
  vary, NAME=kKD, step = 0.0001;
  constraint, range=#E, MUX=Qx, MUY=Qy;
  lmdif, calls = 10000, tolerance = 1.0E-21;
 ENDMATCH;


/******************************************************************************************
 * TWISS
 ******************************************************************************************/

 PSHIFT=0;

/************************************************************
 * MAD uses pt as the 5th variable NOT delta-p.
 * The result is that all derivatives are with repect to pt.
 * This is the reason for the non-standard dispersion values
 * for non-relativistic machines, like the PSB
 ************************************************************/
 select, flag=twiss, clear;
 select, flag=twiss, pattern="^BR.*BPM[^T]",column=name, s, l, betx, bety, mux, muy, angle, k1l, k2l,k3l, x,y, r11, r12, r21, r22, alfx, alfy, dx, dpx,dpy,px,py,phix,phiy;
 twiss, centre, DELTAP = %(DPP)s, sequence=psb%(RING)s, file="%(OUTPUT)s/twiss.dat";

 select, flag=twiss, clear;
 select, flag=twiss,column=name, s, keyword, l, betx, bety, mux, muy, angle, k1l, k2l,k3l, x,y, r11, r12, r21, r22, alfx, alfy, dx, dpx,dpy,px,py,phix,phiy;
 twiss, centre, DELTAP = %(DPP)s, sequence=psb%(RING)s, file="%(OUTPUT)s/twiss_elements.dat";

 ac_s = %(USE_ACD)s;
 if (ac_s == 1) {
      select, flag=twiss, clear;
      twiss;

      betxac = table(twiss, hacmap, betx);
      betyac = table(twiss, vacmap, bety);

      hacmap21 := ac_s * (2*(cos(2*pi*Qxd)-cos(2*pi*Qx))/(betxac*sin(2*pi*Qx)));
      vacmap43 := ac_s * (2*(cos(2*pi*Qyd)-cos(2*pi*Qy))/(betyac*sin(2*pi*Qy)));

      select, flag=twiss, clear;
      select, flag=twiss, pattern="^BR.*BPM",column=name, s, l, betx, bety, mux, muy, angle, k1l, k2l,k3l, x,y, r11, r12, r21, r22, alfx, alfy, dx, dpx,dpy,px,py,phix,phiy;
      twiss, centre, DELTAP = 0, sequence=psb%(RING)s, file="%(OUTPUT)s/twiss_ac.dat";
 }
