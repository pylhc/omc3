system, "mkdir output";

/******************************************************************************************
 *
 * MAD-X input script for the injection optics of the LHC cycle.
 *
 * 20/11/2020 - Fanouria Antoniou, Hannes Bartosik, Chiara Bracco, 
 * Gian Piero di Giovanni, Alexander Huschauer, Elisabeth Renner
 ******************************************************************************************/

 /******************************************************************
 * Energy and particle type definition
 ******************************************************************/
 
call, file="psb_inj_lhc.beam";
BRHO      := BEAM->PC * 3.3356;

set,  format="20.10f";

/******************************************************************
 * Call lattice files
 ******************************************************************/

call, file = 'psb.seq';
call, file = 'psb_aperture.dbx';
call, file = 'psb_inj_lhc.str';
call, file = 'macros.madx';

/******************************************************************
 * Seqedit to install matching marker for beta-beat correction
 ******************************************************************/

use, sequence = psb1;
seqedit, sequence=psb1;
  matchingmarker: marker;
  flatten;
  install, element= matchingmarker, at = 1.32695+BR.BHZ82->L/2, from=BR.BHZ82;
  flatten;
endedit;

/******************************************************************
 * Match reference ratio of KSW magnets at maximum amplitude at 
 * WP Qx = 4.28, Qy = 4.45 according to EDMS2117053, Section 3.2 
 ******************************************************************/ 

Qx = 4.28;
Qy = 4.45;

kBRQD3CORR = 0;
kBRQD14CORR = 0;

exec, unassign_KSW_BSW;

use, sequence = psb1;
exec, match_tunes(Qx, Qy);

exec, assign_KSW_strength;

MATCH, USE_MACRO;
  VARY, NAME = kBRQF, STEP = 1e-6;
  VARY, NAME = kBRQD, STEP = 1e-6;
  VARY, NAME = KSW1L4,  STEP=1.E-8, UPPER= 0.002;
  VARY, NAME = KSW2L1,  STEP=1.E-8, UPPER= 0.00665;
  VARY, NAME = KSW16L1, STEP=1.E-8, UPPER= 0.00665;
  VARY, NAME = KSW16L4, STEP=1.E-8, UPPER= 0.002;
  USE_MACRO, name = ptc_twiss_macro(2,0,0);
  CONSTRAINT, EXPR = Table(ptc_twiss, PSB1$END, MU1) = Qx;
  CONSTRAINT, EXPR = Table(ptc_twiss, PSB1$END, MU2) = Qy;
  CONSTRAINT, EXPR = Table(ptc_twiss, BI1.TSTR1L1, X) = -0.035;
  CONSTRAINT, EXPR = Table(ptc_twiss, BI1.TSTR1L1, PX) = 0.00;
  CONSTRAINT, EXPR = Table(ptc_twiss, BR.BHZ21, X) = 0.00;
  CONSTRAINT, EXPR = Table(ptc_twiss, BR.BHZ21, PX) = 0.00;
  jacobian, calls=50000, bisec=3, tolerance=1e-15;
ENDMATCH; 

/******************************************************************
 * Tune matching and extraction of reference beta functions at 
 * matching marker
 ******************************************************************/

Qx = 4.40;
Qy = 4.45;

exec, match_tunes(Qx, Qy);

MATCH, USE_MACRO;
  VARY, NAME = kBRQF, STEP = 1e-6;
  VARY, NAME = kBRQD, STEP = 1e-6;
  USE_MACRO, name = ptc_twiss_macro(2,0,0);
  CONSTRAINT, EXPR=Table(ptc_twiss, PSB1$END, MU1) = Qx;
  CONSTRAINT, EXPR=Table(ptc_twiss, PSB1$END, MU2) = Qy;
  jacobian, calls=50000, bisec=3, tolerance=1e-15;
ENDMATCH; 

exec, ptc_twiss_macro(2,0,0);
bety_target = Table(ptc_twiss, matchingmarker, BETA22);
alfy_target = Table(ptc_twiss, matchingmarker, ALFA22);

/******************************************************************
 * Match KSW bump for new working point 
 ******************************************************************/ 

MATCH, USE_MACRO;
  VARY, NAME = kBRQF, STEP = 1e-6;
  VARY, NAME = kBRQD, STEP = 1e-6;
  VARY, NAME = ksw_factor, STEP = 1.E-8;
  USE_MACRO, name = ptc_twiss_macro(2,0,0);
  CONSTRAINT, EXPR = Table(ptc_twiss, PSB1$END, MU1) = Qx;
  CONSTRAINT, EXPR = Table(ptc_twiss, PSB1$END, MU2) = Qy;
  CONSTRAINT, EXPR = Table(ptc_twiss, BI1.TSTR1L1, X) = -0.035;
  jacobian, calls=50000, bisec=3, tolerance=1e-18;
ENDMATCH;

/******************************************************************
* Knob for the KSW bump
******************************************************************/

exec, ksw_knob_factors;

/******************************************************************
 * Enabling the BSW bump at maximum amplitude and correcting the 
 * beta-beating
 ******************************************************************/

exec, assign_BSW_strength;
exec, assign_BSW_alignment;

 MATCH, USE_MACRO;
  VARY, NAME = kBRQD3CORR,  STEP = 1e-6;
  VARY, NAME = kBRQD14CORR, STEP = 1e-6;
  VARY, NAME = kBRQF, STEP = 1e-6;
  VARY, NAME = kBRQD, STEP = 1e-6;  
  USE_MACRO, name = ptc_twiss_macro(2,0,0);
  Constraint, EXPR = Table(ptc_twiss, matchingmarker, BETA22) = bety_target;
  Constraint, EXPR = Table(ptc_twiss, matchingmarker, ALFA22) = alfy_target;
  CONSTRAINT, EXPR = Table(ptc_twiss, PSB1$END, MU1) = Qx;
  CONSTRAINT, EXPR = Table(ptc_twiss, PSB1$END, MU2) = Qy;
  jacobian, calls=50000, bisec=3, tolerance=1e-18;
 ENDMATCH;

/******************************************************************
* Knobs for bump at the shavers
******************************************************************/

exec, shaver_bump_knob_factors();

exec, write_str_file("./output/psb_inj_lhc.str");

/******************************************************************
 * PTC Twiss
 ******************************************************************/ 

exec, ptc_twiss_macro(2,0,1);
exec, write_ptc_twiss("./output/psb_inj_lhc.tfs");
